// Makes a VS Code project for developing and testing examples.
package karl2d_make_vscode_project

import "core:os"
import "core:fmt"

main :: proc() {
	make_dir_err := os.make_directory_all(".vscode")
	
	if make_dir_err != nil && make_dir_err != .Exist {
		fmt.eprintfln("Failed to create .vscode directory: %v", make_dir_err)
		return
	}
	
	SETTINGS_TEMPLATE ::
`{
	"editor.tabSize": 4,
	"editor.insertSpaces": true,
	"editor.formatOnSave": false
}
`

	write_settings_err := os.write_entire_file(".vscode/settings.json", SETTINGS_TEMPLATE)
	
	if write_settings_err != nil {
		fmt.eprintfln("Failed to write settings.json: %v", write_settings_err)
		return
	}
	
	launchh, launchh_err := os.open(".vscode/launch.json", {.Write, .Create, .Trunc})
	
	if launchh_err != nil {
		fmt.eprintfln("Failed to create launch.json: %v", launchh_err)
		return
	}
	
	tasksh, tasksh_err := os.open(".vscode/tasks.json", {.Write, .Create, .Trunc})
	
	if tasksh_err != nil {
		fmt.eprintfln("Failed to create tasks.json: %v", tasksh_err)
		return
	}
	
	examples_entries, examples_entries_err := os.read_all_directory_by_path("examples", context.allocator)
	
	if examples_entries_err != nil {
		fmt.eprintfln("Failed to read examples directory: %v", examples_entries_err)
		return
	}
	
	fmt.fprintln(tasksh, "{")
	fmt.fprintln(tasksh, "\t\"version\": \"2.0.0\",")
	fmt.fprintln(tasksh, "\t\"tasks\": [")
	fmt.fprintln(launchh, "{")
	fmt.fprintln(launchh, "\t\"version\": \"0.2.0\",")
	fmt.fprintln(launchh, "\t\"configurations\": [")
		
	name_with_ext :: proc(name: string) -> string {
		return fmt.tprintf("%s.%s", name, ODIN_OS == .Windows ? "exe" : "bin")
	}

	write_debug_tasks_entry :: proc(
		tasks_file: ^os.File,
		launch_file: ^os.File,
		name: string,
		src: string,
		launch_from_root := false
	) {		
		TASKS_ENTRY_TEMPLATE ::
`		{{
			"label": "build %s",
			"type": "shell",
			"command": "odin",
			"args": ["build", "%s", "-debug", "-vet", "-strict-style", "-vet-tabs", "-out:bin/%s"],
			"group": {{
				"kind": "build",
			}}
		}},
`
	
		tasks_entry := fmt.tprintf(
			TASKS_ENTRY_TEMPLATE,
			name,
			src,
			name_with_ext(name),
		)
		
		fmt.fprint(tasks_file, tasks_entry)

		TASKS_ENTRY_GL_TEMPLATE ::
`		{{
			"label": "build %s (GL)",
			"type": "shell",
			"problemMatcher": [
				"$odin"
			],
			"command": "odin",
			"args": ["build", "%s", "-debug", "-vet", "-strict-style", "-vet-tabs", "-out:bin/%s", "-define:KARL2D_RENDER_BACKEND=gl"],
			"group": {{
				"kind": "build",
			}}
		}},
`
		gl_tasks_entry := fmt.tprintf(
			TASKS_ENTRY_GL_TEMPLATE,
			name,
			src,
			name_with_ext(name),
		)
		
		fmt.fprint(tasks_file, gl_tasks_entry)

		// add web build task
		{
			WEB_TASKS_ENTRY_TEMPLATE ::
`		{{
			"label": "build %s (web)",
			"type": "shell",
			"problemMatcher": [
				"$odin"
			],
			"command": "odin run build_web -vet -strict-style -vet-tabs -- %s -vet -strict-style -vet-tabs",
			"group": {{
				"kind": "build",
			}}
		}},
`
	
			web_tasks_entry := fmt.tprintf(
				WEB_TASKS_ENTRY_TEMPLATE,
				name,
				src,
			)
			
			fmt.fprint(tasks_file, web_tasks_entry)
		}

		cwd := launch_from_root ? "${workspaceFolder}" : "${workspaceFolder}/bin"
		
		LAUNCH_ENTRY_TEMPLATE ::
`		{{
			"name": "%s",
			"type": "%s",
			"request": "launch",
			"program": "${{workspaceFolder}}/bin/%s",
			"args": [],
			"cwd": "%s",
			"preLaunchTask": "build %s",
		}},
`

		launch_entry := fmt.tprintf(
			LAUNCH_ENTRY_TEMPLATE,
			name,
			ODIN_OS == .Windows ? "cppvsdbg" : "lldb",
			name_with_ext(name),
			cwd,
			name,
		)
		
		fmt.fprint(launch_file, launch_entry)

		LAUNCH_ENTRY_GL_TEMPLATE ::
`		{{
			"name": "%s (GL)",
			"type": "%s",
			"request": "launch",
			"program": "${{workspaceFolder}}/bin/%s",
			"args": [],
			"cwd": "%s",
			"preLaunchTask": "build %s (GL)",
		}},
`
		gl_launch_entry := fmt.tprintf(
			LAUNCH_ENTRY_GL_TEMPLATE,
			name,
			ODIN_OS == .Windows ? "cppvsdbg" : "lldb",
			name_with_ext(name),
			cwd,
			name,
		)
		
		fmt.fprint(launch_file, gl_launch_entry)
	}
	
	for e in examples_entries {
		if e.type != .Directory {
			continue
		}

		write_debug_tasks_entry(tasksh, launchh, e.name, fmt.tprintf("examples/%v", e.name))
	}
	
	write_debug_tasks_entry(tasksh, launchh, "scrap", "scrap")
	write_debug_tasks_entry(tasksh, launchh, "test_examples", "tools/test_examples")
	write_debug_tasks_entry(tasksh, launchh, "api_doc_builder", "tools/api_doc_builder", true)
	write_debug_tasks_entry(tasksh, launchh, "api_verifier", "tools/api_verifier", true)
	write_debug_tasks_entry(tasksh, launchh, "make_vscode_project", "tools/make_vscode_project", true)
	
	fmt.fprintln(tasksh)
	fmt.fprintln(tasksh, "\t]")
	fmt.fprintln(tasksh, "}")
	fmt.fprintln(launchh)
	fmt.fprintln(launchh, "\t]")
	fmt.fprintln(launchh, "}")
	os.close(launchh)
	os.close(tasksh)
}
